apply plugin: 'war'
apply plugin: 'eclipse-wtp'

sourceCompatibility = 1.8

configurations {
    couchdb
    slf4j
}


dependencies {
    providedCompile group:'javax.websocket', name:'javax.websocket-api', version:'1.1'
    providedCompile group:'javax.ws.rs', name:'javax.ws.rs-api', version:'2.0'
    providedCompile group:'javax.json', name:'javax.json-api', version:'1.0'
    providedCompile 'javax.servlet:javax.servlet-api:3.1.0'
    providedCompile 'javax.annotation:javax.annotation-api:1.2'
    providedCompile 'javax.inject:javax.inject:1'
    providedCompile 'javax.enterprise:cdi-api:1.2'
    providedCompile 'io.swagger:swagger-annotations:1.5.0'

    // couchdb dependencies
    couchdb('org.ektorp:org.ektorp:1.4.2') {
        exclude group: 'org.slf4j'
    }

    // route ektorp slf4j logs to java.util.logging configured by Liberty in server.xml
    slf4j 'org.slf4j:slf4j-jdk14:1.7.13'

    providedCompile configurations.couchdb
    providedCompile configurations.slf4j

    testCompile 'junit:junit:4.12'
}

task exportLibs(type: Sync) {
  into "$buildDir/libs/"
  from configurations.couchdb
  from configurations.slf4j
}

build.dependsOn(exportLibs)

// Set the Eclipse facets to use 3.1 of the Dynamic Web Module which requires Java 1.7 by default.
// Also include the JAX-RS and javascript
eclipse.wtp.facet {
    // Clear default facets to work around bug where you get duplicates including wst 2.4
    facets = []
    facet name: 'jst.java', type: 'fixed'
    facet name: 'jst.web', type: 'fixed'
    facet name: 'jst.web', version: '3.1'
    facet name: 'jst.java', version: '1.8'
    facet name: 'jst.jaxrs', version: '2.0'
    facet name: 'wst.jsdt.web', type: 'fixed'
    facet name: 'wst.jsdt.web', version: '1.0'
}

// Add additional code conventions
eclipse.jdt.file {
    withProperties {
      it['org.eclipse.jdt.core.formatter.indentation.size'] = '4'
      it['org.eclipse.jdt.core.formatter.tabulation.size'] = '4'
      it['org.eclipse.jdt.core.formatter.tabulation.char'] = 'space'
      it['org.eclipse.jdt.core.formatter.indent_switchstatements_compare_to_cases'] = 'true'
      it['org.eclipse.jdt.core.formatter.indent_switchstatements_compare_to_switch'] = 'true'
    }
}
